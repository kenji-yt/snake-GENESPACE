## Check minimum Snakemake version

from snakemake.utils import min_version

min_version("5.20.1")

## Import required libraries
import os

## Configuration file check

if len(config) == 0:
    if os.path.isfile("config/config.yaml"):

        configfile: "config/config.yaml"

    else:
        sys.exit(
            f"Make sure there is a config.yaml file in {os.getcwd()} or specify one with the --configfile commandline parameter."
        )


## Parse config file

OUTPUT_DIR = os.path.normpath(config["OUTPUT"])
INPUT_DIR = os.path.normpath(config["INPUT"])
SCRIPTS_DIR = os.path.normpath("workflow/scripts/") # How to call the scripts during modularization?

## Define number of cores for parsing
count = 0
for item in os.listdir(f"{INPUT_DIR}"):
    item_path = os.path.join(f"{INPUT_DIR}", item)
    # Check if the item is a directory and not excluded
    if os.path.isdir(item_path) and item not in ["beb", "peptide"]:
        count += 1
genespace_parse_threads = count if count < workflow.cores else workflow.cores



## Rules

rule all:
    input:
        genespace_run_log=f"{OUTPUT_DIR}/logs/genespace/genespace_run.log",


rule run_genespace:
    input:
        mc_scan_install_log=f"{OUTPUT_DIR}/logs/genespace/MCScanX/MScanX_install.log", 
        genespace_run_dir=f"{OUTPUT_DIR}/genespace/run_dir",
    params:
        mc_scan_dir=f"{OUTPUT_DIR}/genespace/MCScanX/",
    log:
        f"{OUTPUT_DIR}/logs/genespace/genespace_run.log",
    threads:workflow.cores
    conda:
        "../envs/genespace.yaml",
    script:
        "scripts/run_genespace.R" 


rule parse_genespace_input:
    input:
        input_dir=f"{INPUT_DIR}",
    output:
        genespace_run_dir=directory(f"{OUTPUT_DIR}/genespace/run_dir"),
    params:
        scripts=f"{SCRIPTS_DIR}"
    log:
        f"{OUTPUT_DIR}/logs/genespace/parse_genespace_input.log", # Nothing going it 2> due to agat
    threads:genespace_parse_threads
    conda:
        "../envs/genespace.yaml",
    shell:
        "bash {params.scripts}/parse_genespace_input.sh -i {input.input_dir} -o {output.genespace_run_dir} -l {log} -c {threads}" 



rule install_MCScanX:
    input:
        mc_scan_dir=f"{OUTPUT_DIR}/genespace/MCScanX",
    log:
        f"{OUTPUT_DIR}/logs/genespace/MCScanX/MScanX_install.log",
    conda:
        "../envs/genespace.yaml",
    shell:
        "make -C {input.mc_scan_dir}" 


rule download_MCScanX:
    output:
        mc_scan_dir=directory(f"{OUTPUT_DIR}/genespace/MCScanX"),
    log:
        f"{OUTPUT_DIR}/logs/genespace/MCScanX/MCScanX_dowload.log",
    conda:
        "../envs/genespace.yaml",
    shell:
        "git clone https://github.com/wyp1125/MCScanX {output.mc_scan_dir}"





